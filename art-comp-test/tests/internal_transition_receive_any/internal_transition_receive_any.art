protocol Events {
    out error();
    out e1();
    out e2();
};

capsule Top {
    [[rt::impl_preface]]
    `
        #include "testlib.art.h"
    `
    [[rt::decl]]
    `
        private: bool t1 = false, t2 = false;
    `

    behavior port p1 : Events;
    behavior port p2~ : Events;
    connect p1 with p2;    

    statemachine {
        state Composite {
            state N1, N2;
            entrypoint ini;
            ini -> N1;
            t1: N1 -> N2 on p2.e1
            `
                std::cout << "transition N1 --> N2" << std::endl;
                t1 = true;
            `;
            t2: N2 -> N1 on p2.e2
            `
                std::cout << "transition N2 --> N1" << std::endl;
                t2 = true;
            `;
            onError: on p2.* // Receive any event on p2
            `
                std::cout << "internal transition onError" << std::endl;
                ASSERT(t1 && t2, "Both t1 and t2 should have executed");
                PASS();
            `;
        };
        initial -> Composite.ini
        `
            p1.e1().send();
            p1.e2().send();
            p1.error().send();
        `;
    };
};