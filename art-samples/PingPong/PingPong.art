protocol PROTO {
    /* In Events */
    in pong();
    /* Out Events */
    out ping(`int`);
};

capsule Top {
    /* Ports */
    /* Parts */
    part ping : Pinger, pong : Ponger;
    /* Connectors */
    connect ping.pingPort with pong.pongPort;
    /* State Machine */
    statemachine {
        state State1;
        initial -> State1;
    };
};

capsule Pinger {
    [[rt::decl]]
    `
        private: int count = 0;
    `
    /* Ports */
    behavior port log : Log;
    behavior port timing : Timing;
    service behavior port pingPort : PROTO;
    /* Parts */
    /* Connectors */
    /* State Machine */
    statemachine {
        state State1 {
            pong: on pingPort.pong
            `
            timing.informIn(RTTimespec(1,0));
            `;
            timeout: on timing.timeout
            `
            log.show(getName());
            log.log(" sends ping");
            log.commit();
            pingPort.ping(++count).send();
            `;
        };
        initial -> State1
        `
        log.show(getName());
        log.log(" sends ping");
        log.commit();
        pingPort.ping(count).send();
        `;
    };
};

capsule Ponger {
    [[rt::impl_preface]]
    `
        #include <iostream>
    `
    /* Ports */
    service behavior port pongPort~ : PROTO;
    /* Parts */
    /* Connectors */
    /* State Machine */
    statemachine {
        state State1 {
            ping: on pongPort.ping
            `
            int i = *((int*) rtdata);
            std::cout << getName() << " sends pong (count=" << i << ")" << std::endl;
            pongPort.pong().send();
            `;
        };
        initial -> State1;
    };
};
